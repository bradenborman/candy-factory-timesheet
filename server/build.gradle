plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'war'
}

group = 'borman'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework:spring-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-devtools'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

	// For Excel
	compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.9'

	//For emails
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	//DB config
	compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.16'
	compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.2.2'
	// https://mvnrepository.com/artifact/org.hibernate/hibernate-core

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
	exclude '**/*integration/**'
}

task copyClient(dependsOn: ":client:clientBuild") {
	doLast {
		copy {
			from new File(project(":client").getProjectDir(), "build")
			into "${buildDir}/resources/main/static"
		}
		copy {
			from new File(project(":client").getProjectDir(), "build/index.html")
			into "${buildDir}/resources/main/templates"
		}
		copy {
			from new File(project(":client").getProjectDir(), "build/error.html")
			into "${buildDir}/resources/main/templates"
		}
	}
}

task finalizeBuild(type: Copy, dependsOn: build) {
	from "${buildDir}/libs/server-${version}.war"
	into "${buildDir}/libs"
	rename "server-${version}.war", "candy-factory-timesheet.war"
	delete "server-${version}.war"
}

bootWar.dependsOn(copyClient)
clean.dependsOn(":client:clientClean")
build.finalizedBy(finalizeBuild)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean